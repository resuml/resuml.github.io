<?xml version="1.0" encoding="UTF-8"?> 
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"> 
  <xsl:output encoding="UTF-8" indent="yes"/>

  <xsl:template match="/resume">
    <html>
      <head>
        <title><xsl:value-of select="demographics/name"/> | Resume</title>
        <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/materialize/1.0.0/css/materialize.min.css"/>
        <link rel="stylesheet" href="https://fonts.googleapis.com/icon?family=Material+Icons"/>
        <link rel="stylesheet" href="resume.css"/>
        <link rel="stylesheet" href="resume-print.css"/>
        <link rel="shortcut icon" type="image/png" href="/favicon.png">
          <xsl:attribute name="href">
            <xsl:call-template name="icon-link">
              <xsl:with-param name="source" select="demographics"/>
              <xsl:with-param name="size" select="'64'"/>
            </xsl:call-template>
          </xsl:attribute>
        </link>
      </head>
      <body>
        <div id="print">
          <a class="modal-trigger btn-small waves-effect waves-light" href="#modalPrint"><i class="material-icons left">print</i> Print</a>
        </div>
        <div class="container">
          <div class="row">
            <div class="col s10 offset-s1">
              <xsl:apply-templates/>
            </div>
          </div>
        </div>
          <div id="modalPrint" class="modal">
            <div class="modal-content">
              <h5>Please select the sections you would like to print:</h5>
              <div id="print-sections" class="row"></div>
            </div>
            <div class="modal-footer">
              <a class="modal-close waves-effect waves-green btn-flat">Close</a>
              <a class="waves-effect waves-light btn-small" onclick="printSections();"><i class="material-icons left">print</i> Print</a>
            </div>
          </div>
        <script src="https://cdnjs.cloudflare.com/ajax/libs/jquery/3.3.1/jquery.min.js"></script>
        <script src="https://cdnjs.cloudflare.com/ajax/libs/materialize/1.0.0/js/materialize.min.js"></script>
        <script src="https://cdnjs.cloudflare.com/ajax/libs/moment.js/2.24.0/moment.min.js"></script>
        <script src="resume.js"></script>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="/resume/demographics">
    <div id="demographics" class="row section">
      <div class="header hidden">Contact Information</div>
      <div class="col s10">
        <h1><xsl:value-of select="name"/></h1>
        <h2><xsl:value-of select="title"/></h2>
        <xsl:call-template name="contact"/>
        <p class="description"><xsl:value-of select="description"/></p>
      </div>
      <div class="col s2 center-align">
        <img id="picture" src="{picture}" alt="{name}"/>
      </div>
    </div>
  </xsl:template>

  <xsl:template match="/resume/keywords">
    <div id="keywords" class="row section no-print">
      <xsl:for-each select="./*">
        <div class="header"><xsl:value-of select="@title"/></div>
        <div class="col s12">
          <xsl:call-template name="tags"/>
        </div>
      </xsl:for-each>
    </div>
  </xsl:template>

  <xsl:template match="/resume/experience">
    <div id="experience" class="row section">
      <div class="header">Experience</div>
      <div class="col s12">
        <xsl:for-each select="company">
          <div class="row company">
            <div class="col s1 center-align">
              <xsl:call-template name="icon"/>
            </div>
            <div class="col s11">
              <xsl:choose>
                <xsl:when test="count(role) > 1">
                  <h2 class="name"><a href="{link}" target="_blank"><xsl:value-of select="name"/></a></h2>
                  <xsl:call-template name="tenure">
                    <xsl:with-param name="type" select="'duration'"/>
                  </xsl:call-template>
                  <div class="roles">
                    <xsl:for-each select="role">
                      <div class="role">
                        <h3 class="title"><xsl:value-of select="title"/></h3>
                        <xsl:call-template name="tenure">
                          <xsl:with-param name="type" select="'dates'"/>
                        </xsl:call-template>
                        <xsl:call-template name="tags"/>
                        <xsl:call-template name="description"/>
                      </div>
                    </xsl:for-each>
                  </div>
                </xsl:when>
                <xsl:otherwise>
                  <role>
                    <h2><xsl:value-of select="role/title"/></h2>
                    <h3 class="name"><a href="{link}" target="_blank"><xsl:value-of select="name"/></a></h3>
                    <xsl:call-template name="tenure"/>
                    <xsl:call-template name="tags"/>
                    <xsl:call-template name="description"/>
                  </role>
                </xsl:otherwise>
              </xsl:choose>
            </div>
          </div>
        </xsl:for-each>
      </div>
    </div>
  </xsl:template>

  <xsl:template match="/resume/education">
    <div id="education" class="row section">
      <div class="header">Education</div>
      <div class="col s12">
        <xsl:for-each select="./degree">
          <div class="row degree">
            <div class="col s1">
              <xsl:call-template name="icon"/>
            </div>
            <div class="col s11">
              <h2 class="title"><xsl:value-of select="title"/></h2>
              <h3 class="institution"><a href="{link}" target="_blank"><xsl:value-of select="institution"/></a></h3>
              <xsl:call-template name="tenure"/>
              <xsl:call-template name="tags"/>
              <xsl:call-template name="description"/>
            </div>
          </div>
        </xsl:for-each>
      </div>
    </div>
  </xsl:template>

  <xsl:template match="/resume/projects">
    <div id="projects" class="row section no-print">
      <div class="header">Projects</div>
      <div class="col s12">
        <xsl:for-each select="./project">
          <div class="row project">
            <div class="col s1">
              <xsl:call-template name="icon"/>
            </div>
            <div class="col s11">
              <h2 class="name"><a href="{link}" target="_blank"><xsl:value-of select="name"/></a></h2>
              <p class="date"><xsl:value-of select="date"/></p>
              <xsl:call-template name="tags"/>
              <xsl:call-template name="description"/>
            </div>
          </div>
        </xsl:for-each>
      </div>
    </div>
  </xsl:template>

  <xsl:template match="/resume/publications">
    <div id="publications" class="row section no-print">
      <div class="header">Publications</div>
      <div class="col s12">
        <xsl:for-each select="./publication">
          <div class="row publication">
            <div class="col s12">
              <h2 class="name"><a href="{link}" target="_blank"><xsl:value-of select="title"/></a></h2>
              <h3>
                <span class="publisher"><xsl:value-of select="publisher"/></span>
                -
                <span class="date"><xsl:value-of select="available"/></span>
              </h3>
              <xsl:call-template name="description"/>
            </div>
          </div>
        </xsl:for-each>
      </div>
    </div>
  </xsl:template>

  <xsl:template match="/resume/credits">
    <div id="credits" class="row">
      <div class="header"></div>
      <div class="col s12">
        <xsl:for-each select="credit">
          <a href="{link}" target="_blank">
            <img alt="{title}">
              <xsl:attribute name="src">
                <xsl:call-template name="icon-link">
                  <xsl:with-param name="source" select="."/>
                  <xsl:with-param name="size" select="'16'"/>
                </xsl:call-template>
              </xsl:attribute>
            </img>
            <xsl:value-of select="title"/>
          </a>
        </xsl:for-each>
      </div>
    </div>
  </xsl:template>

  <xsl:template name="icon-link">
    <xsl:param name="source"/>
    <xsl:param name="size"/>
    <xsl:variable name="icon-link">
      <xsl:choose>
        <xsl:when test="$source/icon"><xsl:value-of select="$source/icon"/></xsl:when>
        <xsl:otherwise>https://api.faviconkit.com/<xsl:value-of select="substring-after($source/link, '://')"/>/<xsl:value-of select="$size"/></xsl:otherwise>
      </xsl:choose>
    </xsl:variable>
    <xsl:value-of select="$icon-link"/>
  </xsl:template>

  <xsl:template name="icon">
    <a class="icon" href="{link}" target="_blank">
      <img alt="{employer|title|name}">
        <xsl:attribute name="src">
          <xsl:call-template name="icon-link">
            <xsl:with-param name="source" select="."/>
            <xsl:with-param name="size" select="'64'"/>
          </xsl:call-template>
        </xsl:attribute>
      </img>
    </a>
  </xsl:template>

  <xsl:template name="tenure">
    <xsl:param name="type" select="'full'"/>
    <xsl:variable name="end">
      <xsl:choose>
        <xsl:when test=".//tenure/end"><xsl:value-of select="(.//tenure/end)[1]"/></xsl:when>
        <xsl:otherwise>Current</xsl:otherwise>
      </xsl:choose>
    </xsl:variable>

    <xsl:choose>
      <xsl:when test="$type = 'duration'">
        <p class="tenure short">
          <span class="date start"><xsl:value-of select="(.//tenure/start)[last()]"/></span>
          <span class="date end"><xsl:value-of select="$end"/></span>
          <span class="duration"></span>
        </p>
      </xsl:when>
      <xsl:when test="$type = 'dates'">
        <p class="tenure">
          <span class="date start"><xsl:value-of select="(.//tenure/start)[last()]"/></span>
          <span class="date end"><xsl:value-of select="$end"/></span>
        </p>
      </xsl:when>
      <xsl:otherwise>
        <p class="tenure">
          <span class="date start"><xsl:value-of select="(.//tenure/start)[last()]"/></span>
          <span class="date end"><xsl:value-of select="$end"/></span>
          <span class="duration"></span>
        </p>
      </xsl:otherwise>
    </xsl:choose>
  </xsl:template>

  <xsl:template name="tags">
    <xsl:variable name="tags" select="(.//tags|.)"/>

    <div class="tags">
      <xsl:for-each select="$tags/tag">
        <div class="tag"><xsl:value-of select="."/></div>
      </xsl:for-each>
    </div>
  </xsl:template>

  <xsl:template name="description">
    <p class="description"><xsl:value-of select=".//description"/></p>
    <ul class="details">
      <xsl:for-each select=".//details/detail">
        <li><xsl:value-of select="."/></li>
      </xsl:for-each>
    </ul>
  </xsl:template>

  <xsl:template name="contact">
    <div id="contact">
      <xsl:if test="address">
        <p class="address"><i class="tiny material-icons">location_on</i> <xsl:value-of select="address"/></p>
      </xsl:if>

      <xsl:if test="phone">
        <p class="phone"><i class="tiny material-icons">local_phone</i> <xsl:value-of select="phone"/></p>
      </xsl:if>

      <xsl:if test="email">
        <p class="email">
          <a href="mailto:{email}">
            <i class="tiny material-icons">mail_outline</i>
            <xsl:value-of select="email"/>
          </a>
          <xsl:if test="gpg">
            <a class="public-key" href="{gpg/key}" target="_blank">
              <xsl:value-of select="gpg/fingerprint"/>
            </a>
          </xsl:if>
        </p>
      </xsl:if>

      <xsl:for-each select=".//links/link">
        <xsl:variable name="type">
          <xsl:choose>
            <xsl:when test="count(@type) > 0"><xsl:value-of select="@type"/></xsl:when>
            <xsl:otherwise>link</xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <xsl:variable name="icon">
          <xsl:choose>
            <xsl:when test="count(@icon) > 0"><xsl:value-of select="@icon"/></xsl:when>
            <xsl:otherwise>insert_link</xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <xsl:variable name="title">
          <xsl:choose>
            <xsl:when test="string-length(.) > 0"><xsl:value-of select="."/></xsl:when>
            <xsl:otherwise><xsl:value-of select="@href"/></xsl:otherwise>
          </xsl:choose>
        </xsl:variable>
        <xsl:variable name="print">
          <xsl:choose>
            <xsl:when test="not((@print)) or (@print = 'true')"></xsl:when>
            <xsl:otherwise>no-print</xsl:otherwise>
          </xsl:choose>
        </xsl:variable>

        <p class="{$type} {$print}">
          <a href="{@href}" target="_blank">
            <i class="tiny material-icons"><xsl:value-of select="$icon"/></i>
            <xsl:value-of select="$title"/>
          </a>
        </p>
      </xsl:for-each>
    </div>
  </xsl:template>
</xsl:stylesheet>
